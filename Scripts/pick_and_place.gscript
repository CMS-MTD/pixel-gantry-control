# Steps: 
# 1. Locate Glass Piece
# 2. Record/Locate target position
# 3. Pickup and place glass piece
# 4. Record final position
CLEARLOG
COPY $speed 50

COPY $start_pos {308.871103,397.731020,77.831420}
COPY $start_rot {-0.000670,0.000412,-0.004562,0.999989}

COPY $end_pos {232.732261,396.215568,77.655460}
COPY $end_rot {-0.000300,0.000640,-0.001178,0.999999}

LOADGEODEF $tl_local %v TFPX_1X2_MOCKUP_V2 fid_tl
LOADGEODEF $tr_local %v TFPX_1X2_MOCKUP_V2 fid_tr
LOADGEODEF $br_local %v TFPX_1X2_MOCKUP_V2 fid_br
LOADGEODEF $bl_local %v TFPX_1X2_MOCKUP_V2 fid_bl

SETVAC module_chuck_0_slot_2 1
COPY $pos $start_pos
COPY $rot $start_rot
CALL @SURVEY


LOADTOOL PICKER

#Pickup Step
COPY $target_pos $start_pos
CALL @CALCPICKUP
MOVETO $pickup_pos_z0 50
MOVETO $pickup_pos 10
SETVAC gantry_head_inner 1
SETVAC module_chuck_0_slot_2 0
WAIT 2000
MOVETO $pickup_pos_z0 10

# Place Step
COPY $target_pos $end_pos
CALL @CALCPICKUP
MOVETO $pickup_pos_z0 50
MOVETO $pickup_pos 10
SETVAC gantry_head_inner 0
SETVAC module_chuck_0_slot_1 1
WAIT 2000
MOVETO $pickup_pos_z0 10

UNLOADTOOL
SETVAC module_chuck_0_slot_1 0

END


# SURVEY
# Arguments:
#  $pos - expected position of piece
#  $rot - expected orientation of piece
#  $**_local - local coordinates of the fiducials
@SURVEY MPGON
        MOVETO $pos $speed
        TRANSFORML2G $tl_global $tl_local $pos $rot
        MOVETO $tl_global $speed
        VIDEO
        GETPOS $tl_meas

        TRANSFORML2G $tr_global $tr_local $pos $rot
        MOVETO $tr_global $speed
        VIDEO
        GETPOS $tr_meas

        TRANSFORML2G $br_global $br_local $pos $rot
        MOVETO $br_global $speed
        VIDEO
        GETPOS $br_meas

        TRANSFORML2G $bl_global $bl_local $pos $rot
        MOVETO $bl_global $speed
        VIDEO
        GETPOS $bl_meas

        MPGOFF
        FIT $pos_meas $rot_meas TFPX_1X2_MOCKUP_V2 $tr_meas $br_meas $bl_meas $tl_meas
        PRINT "Measured position: %v" $pos_meas
        PRINT "Measured rotation: %q (%r)" $rot_meas $rot_meas
        GOTO $RET

# CALCPICKUP
# Arguments:
#  $target_pos - center of piece to pickup/place
@CALCPICKUP LOADGEODEF $picker_height %v PICKER_TOOL center_offset
            LOADGEODEF $ghco %v GANTRY_HEAD tool_holder_offset
            ADD $netoffset $picker_height $ghco
            SUB $pickup_pos $target_pos $netoffset
            COPY $pickup_pos_z0 $pickup_pos
            COPY $pickup_pos_z0.z 0
            GOTO $RET