#FINDFID $pos FPIX_MOCK
#PRINT "FIDPOS %v" $pos
#GETPOS $position
#PRINT "GetPos %v" $position
LOADCONFIG
SETLOG pick_and_place.txt
HOME
COPY $speed 150
COPY $img_idx 0

COPY $start_tr  {344.224076,528.473174,67.998338}
COPY $start_tl  {326.790436,528.584280,68.027211}
COPY $start_bl {326.995097,571.774673,68.050822}
COPY $start_br  {344.192747,571.689922,68.023199}

FIT $start_pos $start_rot TFPX_1X2_MOCKUP $start_tr $start_br $start_bl $start_tl

PRINT "STARTPOS|ROT %v %v" $start_pos $start_rot

COPY $end_tr {343.987944,528.580042,68.003568}
COPY $end_tl {250.913168,529.169935,68.303301}
COPY $end_bl {327.006152,571.804537,68.069388}
COPY $end_br  {344.193072,571.778504,68.011344}

FIT $end_pos $end_rot TFPX_1X2_MOCKUP $end_tr $end_br $end_bl $end_tl

FLEXREAD $tl_local geometry.TFPX_1X2_MOCKUP.fid_tl
FLEXREAD $tr_local geometry.TFPX_1X2_MOCKUP.fid_tr
FLEXREAD $br_local geometry.TFPX_1X2_MOCKUP.fid_br
FLEXREAD $bl_local geometry.TFPX_1X2_MOCKUP.fid_bl

@PROCESS_START SETVAC purdue_mechanics_right 1
COPY $pos $start_pos
COPY $rot $start_rot
CALL @SURVEY

LOADTOOL picker_tool


#Pickup Step
COPY $target_pos $meas_pos
CALL @CALCPICKUP
MOVETO $pickup_pos_z0 50
ROTATE $meas_rot
MOVETO $pickup_pos 10
SETVAC gantry_head_inner 1
WAIT 500
SETVAC purdue_mechanics_right 0
WAIT 2000
MOVETO $pickup_pos_z0 10
ROTATE $meas_rot -1

# Place Step
COPY $target_pos $end_pos
CALL @CALCPICKUP
MOVETO $pickup_pos_z0 50
ROTATE $end_rot
MOVETO $pickup_pos 10
SETVAC gantry_head_inner 0
WAIT 500
SETVAC purdue_mechanics_left 1
WAIT 2000
MOVETO $pickup_pos_z0 10
ROTATE $end_rot -1

UNLOADTOOL

COPY $pos $end_pos
COPY $rot $end_rot
CALL @SURVEY

SUB $delta $meas_pos $end_pos
PRINT "Misplacement: %v" $delta

MOVEREL {0,0,-60} $speed
SETVAC purdue_mechanics_left 0

CHOICEPOPUP $choice "Move piece back to launchpad or quit" "I moved it" "Quit" 
GOTOIF @PROCESS_START $choice

END


# SURVEY
# Arguments:
#  $pos - expected position of piece
#  $rot - expected orientation of piece
#  $**_local - local coordinates of the fiducials
@SURVEY COPY $SURVEY_RET $RET
        MPGON
        MOVETO $pos $speed
        TRANSFORML2G $tl_global $tl_local $pos $rot
        MOVETO $tl_global $speed
        VIDEO
        WAIT 1000
        FINDFID $tl_meas FPIX_MOCK
        PRINT "tl_meas %v" $tl_meas
        CALL @SAVE_SNAPSHOT 


        TRANSFORML2G $tr_global $tr_local $pos $rot
        MOVETO $tr_global $speed
        VIDEO
        WAIT 1000
        FINDFID $tr_meas FPIX_MOCK
        PRINT "tr_meas %v" $tr_meas
        CALL @SAVE_SNAPSHOT


        TRANSFORML2G $br_global $br_local $pos $rot
        MOVETO $br_global $speed
        VIDEO
        WAIT 1000
        FINDFID $br_meas FPIX_MOCK
        PRINT "br_meas %v" $br_meas
        CALL @SAVE_SNAPSHOT


        TRANSFORML2G $bl_global $bl_local $pos $rot
        MOVETO $bl_global $speed
        VIDEO
        WAIT 1000
        FINDFID $bl_meas FPIX_MOCK
        PRINT "bl_meas %v" $bl_meas
        CALL @SAVE_SNAPSHOT


        MPGOFF
        FIT $meas_pos $meas_rot TFPX_1X2_MOCKUP $tr_meas $br_meas $bl_meas $tl_meas
        PRINT "Measured position: %v" $meas_pos
        PRINT "Measured rotation: %q (%r)" $meas_rot $meas_rot
        GOTO $SURVEY_RET


# SAVE_SNAPSHOT
# Arguments:
#  $img_idx - numerical suffix of image filename, get's incremented each time
@SAVE_SNAPSHOT SNAPSHOT gantryhead "fiducial_on_glass_{$img_idx}.png"
               INC $img_idx $img_idx
               GOTO $RET


# CALCPICKUP
# Arguments:
#  $target_pos - center of piece to pickup/place
@CALCPICKUP FLEXREAD $picker_height geometry.PICKER_TOOL.center_offset
            FLEXREAD $ghco          geometry.tool_holder_offset
            ADD $netoffset $picker_height $ghco
            SUB $pickup_pos $target_pos $netoffset
            COPY $pickup_pos_z0 $pickup_pos
            COPY $pickup_pos_z0.z 0
            GOTO $RET